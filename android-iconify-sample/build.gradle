apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.joanzapata.android.icons.sample"
        minSdkVersion 14
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
    }

    def password = project.hasProperty('ICONIFY_SAMPLE_KEYSTORE_PASSWORD') ?
            project.property('ICONIFY_SAMPLE_KEYSTORE_PASSWORD') : ''
    signingConfigs {
        release {
            storeFile file("keystore")
            storePassword password
            keyAlias "android-iconify"
            keyPassword password
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    publishing {
        singleVariant("release")
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.joanzapata.iconify'
                artifactId = 'android-iconify-sample'
                version = rootProject.ext.versionName
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.jakewharton:butterknife:7.0.1'

    implementation project(':android-iconify')
    implementation project(':android-iconify-fontawesome')
    implementation project(':android-iconify-typicons')
    implementation project(':android-iconify-ionicons')
    implementation project(':android-iconify-entypo')
    implementation project(':android-iconify-material')
    implementation project(':android-iconify-material-community')
    implementation project(':android-iconify-meteocons')
    implementation project(':android-iconify-weathericons')
    implementation project(':android-iconify-simplelineicons')
}
